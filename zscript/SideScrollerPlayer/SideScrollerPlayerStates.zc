// Animation states for the player
enum EAnimationStates
{
	ANIMSTATE_SPAWN,
	ANIMSTATE_STAND,
	ANIMSTATE_WALKING,
	ANIMSTATE_JUMPING,
	ANIMSTATE_FALLING,
	ANIMSTATE_CROUCHING,
	TOTAL_PLAYER_STATES
}

// This is the actual player sprite that will be seen and animated in-game.
extend class SideScrollerPlayer
{
	const moveThreshold = 0.05;

	void AnimateSideScrollerPlayer(void)
	{
		// retrieve player information
		bool isOnfloor = OnFloor();
		double pSpeed = GetMoveSpeed();

		// set direction
		Scale.Y = spriteScale;
		if (dir == DIR_Left)
		{
			Scale.X = spriteScale;
		}
		else if (dir == DIR_Right)
		{
			Scale.X = -spriteScale;
		}

		////////////////////////////////////////////////////////////////////////
		//
		// STATES
		// Must be assigned in the exact order as EAnimationStates
		//
		////////////////////////////////////////////////////////////////////////
		int StateToPlay;
		StateLabel stateName[TOTAL_PLAYER_STATES];
		stateName[0] = "Spawn";
		stateName[1] = "Stand";
		stateName[2] = "Walking";
		stateName[3] = "Jumping";
		stateName[4] = "Falling";
		stateName[5] = "Crouching";

		// standing still
		if (isOnFloor && pSpeed <= moveThreshold)
		{
			StateToPlay = ANIMSTATE_STAND;
		}

		// walking
		if (isOnFloor && pSpeed > moveThreshold)
		{
			StateToPlay = ANIMSTATE_WALKING;
		}

		// jumping and falling
		if (!isOnFloor)
		{
			if (isJumping)
				StateToPlay = ANIMSTATE_JUMPING;
			else if (isFalling)
				StateToPlay = ANIMSTATE_FALLING;
		}

		// crouching
		if (isCrouching)
		{
			StateToPlay = ANIMSTATE_CROUCHING;
		}

		// set the new animation
		if (oldState != StateToPlay)
		{
			SetStateLabel(stateName[StateToPlay]);
			oldState = StateToPlay;
		}
	}

	States
	{
	Spawn:
	Stand:
		VKIL A -1;
		Loop;
	Walking:
		VKIL BCDEFGHGFEDC 2;
		Loop;
	Jumping:
		VKIL IJ 3;
		VKIL K -1;
		Loop;
	Falling:
		VKIL LM 3;
		VKIL N -1;
		Loop;
	Crouching:
		VKIL O -1;
		Loop;
	}
}

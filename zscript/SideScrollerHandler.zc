class SideScrollerHandler : EventHandler
{
	// setup the camera and other spawning stuff here
	// According to Graf Zahl, BeginPlay on the PlayerPawn is a bit unreliable
	// so we'll do our setup here in the PlayerEntered EventHandler
	// Source: https://mantis.zdoom.org/view.php?id=315#c745

	override void PlayerEntered(PlayerEvent e)
	{
		let pmo = SideScrollerPlayer(players[e.PlayerNumber].mo);
		if (pmo)
		{
			// spawn the camera
			pmo.SpawnCamera();
		}
	}
	
	
	override void PlayerRespawned (PlayerEvent e) // spawn camera also when the player respawns
	{
		let pmo = SideScrollerPlayer(players[e.PlayerNumber].mo);
		if (pmo)
		{
			// spawn the camera
			pmo.SpawnCamera();
		}
	}
	
	// destroy the camera when this level is unloaded
	// in the case of hub levels, we don't want to spawn extra cameras
	// when the player returns to this level
    override void WorldUnloaded(WorldEvent e)
    {
		for (int i = 0; i < MAXPLAYERS; i++)
		{
			if (playeringame[i])
			{
				let pmo = SideScrollerPlayer(players[i].mo);
				if (pmo)
				{
					// destroy the camera
					pmo.DestroyCamera();
				}
			}
		}
	}
}
